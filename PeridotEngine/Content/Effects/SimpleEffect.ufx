#if OPENGL
	#define SV_POSITION POSITION
	#define VS_SHADERMODEL vs_3_0
	#define PS_SHADERMODEL ps_3_0
#else
	#define VS_SHADERMODEL vs_4_0
	#define PS_SHADERMODEL ps_4_0
#endif

#include "Macros.fxh"

DECLARE_TEXTURE(Texture, 0);
DECLARE_TEXTURE(ShadowMap, 1);

matrix World;
matrix View;
matrix ViewProjection;
matrix NormalMatrix;

float4 MixColor;

float2 TexturePosition;
float2 TextureSize;
float2 TextureRepeat;

float3 CameraPosition;

float3 LightPosition;
matrix LightWorldViewProjection;

float ObjectId;

float CalculateShadowPresence(float4 positionLightSpace)
{
    positionLightSpace /= positionLightSpace.w;
    
    float2 uv = float2(
        map(positionLightSpace.x, -1, 1, 0, 1),
        map(-positionLightSpace.y, -1, 1, 0, 1)
    );
    
    float shadowCoeff = 0;
    for (float x = -1.5; x < 1.5; x++)
    {
        for (float y = -1.5; y < 1.5; y++)
        {
            float mapDepth = SAMPLE_TEXTURE(ShadowMap, uv + float2(x, y) * 0.0015).r;
            bool inLight = (mapDepth > (positionLightSpace.z - 0.0025f))
            || uv.x > 1 || uv.x < 0 || uv.y > 1 || uv.y < 0;
            
            shadowCoeff += inLight;
        }
    }
    
    shadowCoeff /= 16;
    
    return shadowCoeff;
}

float CalculateDiffuseShading(float3 position, float3 normal) {
    float3 lightRay = mul(position - LightPosition, View);
    lightRay = normalize(-lightRay);
    normal = normalize(normal);

    return clamp(dot(normal, lightRay), 0, 1);
}

$uberblock SimpleEffect(bool vertexColorEnabled, bool textureEnabled, bool shadowsEnabled, bool diffuseShading, bool randomTextureRotation, bool objectPickingEnabled) constraint (!randomTextureRotation | textureEnabled)

struct PixelOut$$
{
    float4 Color : SV_TARGET0;
    float Depth : SV_TARGET1;
    float4 Normal : SV_TARGET2;

    $if objectPickingEnabled
        float ObjectId : SV_TARGET3;
    $endif
};

struct VertexIn$$
{
	float4 Position : POSITION0;
    $if vertexColorEnabled
        float4 Color : COLOR0;
    $endif
    $if textureEnabled
        float2 TexCoord : TEXCOORD0;
    $endif
    // TODO: Normals should be transmitted always (if mesh has normals), to be used to render the normal buffer
    $if diffuseShading
        float3 Normal : NORMAL0;
    $endif
};

struct PixelIn$$
{
	float4 Position : SV_POSITION;
    float2 Depth : TEXCOORD2;

    $if vertexColorEnabled
        float4 Color : COLOR0;
    $endif
    $if textureEnabled
        float2 TexCoord : TEXCOORD0;
    $endif
    $if shadowsEnabled
        float4 PositionLightSpace : TEXCOORD1;
    $endif
    $if diffuseShading
        float3 Normal : NORMAL0; 
    $endif
    // TODO: Normals should be transmitted always (if mesh has normals), to be used to render the normal buffer
    $if diffuseShading
        float4 WorldPosition : POSITION1;
    $endif
};

PixelIn$$ VertexShader$$(in VertexIn$$ input)
{
    PixelIn$$ output = (PixelIn$$) 0;

    float4 worldPos = mul(input.Position, World);
	output.Position = mul(worldPos, ViewProjection);
    output.Depth = output.Position.zw;

    $if vertexColorEnabled
        output.Color = input.Color;
    $endif
    $if textureEnabled
        output.TexCoord = input.TexCoord;
    $endif
    $if shadowsEnabled
        output.PositionLightSpace = mul(input.Position, LightWorldViewProjection);
    $endif
    $if diffuseShading
        output.Normal = mul(input.Normal, (float3x3)NormalMatrix);
    $endif
    $if diffuseShading
        output.WorldPosition = worldPos;
    $endif

	return output;
}

PixelOut$$ PixelShader$$(PixelIn$$ input)
{
    PixelOut$$ pout = (PixelOut$$)0;
    float4 result = MixColor;

    $if vertexColorEnabled
        result *= input.Color;
    $endif

    $if textureEnabled
        float2 internalTexCoord = frac(input.TexCoord * TextureRepeat);

        float2 texCoord = internalTexCoord;

        // random rotation for each tile for repeating textures
    $endif
    $if randomTextureRotation
        float rand = random(floor(input.TexCoord * TextureRepeat));
        rand *= 4;
        rand = floor(rand);
        texCoord = float2(0, 0);

        // don't rotate
        texCoord.x += internalTexCoord.x * (rand == 0);
        texCoord.y += internalTexCoord.y * (rand == 0);

        // rotate ccw once
        texCoord.x += (1 - internalTexCoord.y) * (rand == 1);
        texCoord.y += (internalTexCoord.x) * (rand == 1);

        // rotate twice
        texCoord.x += (1 - internalTexCoord.x) * (rand == 2);
        texCoord.y += (1 - internalTexCoord.y) * (rand == 2);

        // rotate 3 times
        texCoord.x += (internalTexCoord.y) * (rand == 3);
        texCoord.y += (1 - internalTexCoord.x) * (rand == 3);
    $endif
    $if textureEnabled
        texCoord = TexturePosition + texCoord * TextureSize;
        result *= SAMPLE_TEXTURE(Texture, texCoord);
    $endif

    float lightIntensity = 0;
    $if shadowsEnabled
        lightIntensity = CalculateShadowPresence(input.PositionLightSpace);
    $endif

    $if diffuseShading
        float3 normal = normalize(input.Normal);
        lightIntensity = min(lightIntensity, CalculateDiffuseShading(input.WorldPosition.xyz / input.WorldPosition.w, normal.xyz));
        pout.Normal = float4(normal, 1);
    $endif

    $if shadowsEnabled || diffuseShading
        result = float4(result.rgb * (0.4 + lightIntensity * 0.6), result.a);
    $endif
    
    pout.Color = result;
    pout.Depth = input.Depth.x / input.Depth.y;

    $if objectPickingEnabled
        pout.ObjectId = ObjectId;
    $endif

    return pout;

}
$endblock

$ubercombinations SimpleEffect (bool vertexColorEnabled, bool textureEnabled, bool shadowsEnabled, bool diffuseShading, bool randomTextureRotation, bool objectPickingEnabled) constraint (!randomTextureRotation | textureEnabled) technique SimpleEffect(vertexColorEnabled, textureEnabled, shadowsEnabled, diffuseShading, randomTextureRotation, objectPickingEnabled)